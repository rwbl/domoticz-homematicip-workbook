! File: status-display-set-page.script
! Date: 20210528
! Author: Robert W.B. Linn
!
! If buttons 1-2 of the status display HmIP-WRCD is pressed, send custom event with buttonnr to Domoticz to action accordingly.
! Via cuxd an http api request for a customevent is submitted to domoticz.
! The Domoticz custom event data parameter must be in json format.
! Example: {["channel"]="HmIP-RC8 000B1BE98D94DE:1", ["buttonnr"]=1}
string cAmp = "&";
! Domoticz production system url base for the http api request
string urlBase = "'http://domoticz-ip:port/json.htm";
! string urlBase = "'http://domoticz-ip:port/json.htm";
! Custom event name = MUST match name of the Domoticz dzVents script
string customEvent = "homematic_status_display_set_page";
! Get the object datapoint of the remote control from $src$
object objDatapoint = dom.GetObject ("$src$");
if (objDatapoint) {
    ! Channelobject 1-2 according buttons 1-2, i.e.,  HmIP-WRCD 002A5D8989D599:1 or :2
    var objChannel = dom.GetObject(objDatapoint.Channel());
    ! Get the buttonnr 1-2 from the channel 
    integer buttonNr = (objChannel.Name().StrValueByIndex(":",1)).ToInteger();
    ! Custom event data parameter must be in json format
    string data = '{"channel":"'#objChannel#'","buttonnr":'#buttonNr#'}';
    ! Build the Domoticz http rest request url to update the device using customevent
    string urlRequest = urlBase#"?type=command"#cAmp#"param=customevent"#cAmp#"event="#customEvent#cAmp#"data="#data#"'";
    ! Run the command without a return result
    var cmdRes = dom.GetObject("CUxD.CUX2801001:1.CMD_EXEC").State("wget -q -O - "#urlRequest);
    WriteLine("CMD_EXEC: " # cmdRes);
}
